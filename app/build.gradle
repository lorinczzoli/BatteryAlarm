apply plugin: 'com.android.application'

android {
    compileSdkVersion 26

    def versionPropsFile = file('version.properties')

    if (versionPropsFile.canRead()) {
        def Properties versionProps = new Properties()
        versionProps.load(new FileInputStream(versionPropsFile))

        def ver_major = versionProps['VERSION_MAJOR'].toInteger();
        def ver_minor = versionProps['VERSION_MINOR'].toInteger();
        def ver_patch = versionProps['VERSION_PATCH'].toInteger();

        def ver_build = versionProps['VERSION_BUILD'].toInteger() + 1;
        versionProps['VERSION_BUILD'] = ver_build.toString()

        def date = new Date()
        def ver_date = date.format('yyyy-MM-dd_HHmmss')
        versionProps['VERSION_DATE'] = ver_date.toString()

        def ver_code = (int)date.getTime()

        versionProps.store(versionPropsFile.newWriter(), null)

        defaultConfig {
            applicationId "com.yahoo.lorinczzoli.batteryalarm"
            minSdkVersion 21
            targetSdkVersion 26
            versionCode ver_code
            versionName "${ver_major}.${ver_minor}.${ver_patch}.${ver_build} (Build ${ver_date})"
            testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        }
    }
    else {
        throw new GradleException("Could not read version.properties!")
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
}
